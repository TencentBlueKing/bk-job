/*
 * Tencent is pleased to support the open source community by making BK-JOB蓝鲸智云作业平台 available.
 *
 * Copyright (C) 2021 Tencent.  All rights reserved.
 *
 * BK-JOB蓝鲸智云作业平台 is licensed under the MIT License.
 *
 * License for BK-JOB蓝鲸智云作业平台:
 * --------------------------------------------------------------------
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated
 * documentation files (the "Software"), to deal in the Software without restriction, including without limitation
 * the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and
 * to permit persons to whom the Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or substantial portions of
 * the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO
 * THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF
 * CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
 * IN THE SOFTWARE.
 */

import com.dorongold.gradle.tasktree.TaskTreePlugin

buildscript {
    ext {
        set('springDependencyManagePluginVersion', "1.0.11.RELEASE")
        set("springBootVersion", "2.7.11")
        set("gradleJooqVersion", "3.0.0")
    }

    repositories {
        mavenLocal()

        // 用户通过命令行方式指定的 maven repo
        def mavenRepoUrls = System.getProperty("mavenRepoUrl")
        if (mavenRepoUrls == null || mavenRepoUrls.trim().length() == 0) {
            // gradle.properties 中的 maven repo。优先级从高到低:
            // 1. command line, as set using -D.
            // 2. gradle.properties in GRADLE_USER_HOME directory (<home directory of the current user>/.gradle by default)
            // 3. gradle.properties in the project’s directory, then its parent project’s directory up to the build’s root directory.
            // 4. gradle.properties in Gradle installation directory.
            mavenRepoUrls = project.findProperty("MAVEN_REPO_URL")
        }
        if (mavenRepoUrls != null && mavenRepoUrls.trim().length() > 0) {
            String[] repoUrls = mavenRepoUrls.trim().replace(" ", "").split(",")
            for (String repoUrl : repoUrls) {
                println("Add maven repo:" + repoUrl)
                maven { url repoUrl }
            }
        }

        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:$springDependencyManagePluginVersion"
        classpath "nu.studer:gradle-jooq-plugin:$gradleJooqVersion"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.0'
        classpath "gradle.plugin.com.dorongold.plugins:task-tree:1.5"
        classpath "com.vanniktech:gradle-dependency-graph-generator-plugin:0.8.0"
    }
}

plugins {
    id "java-library"
    id "io.spring.dependency-management" version '1.0.11.RELEASE' apply false
    id 'org.springframework.boot' version '2.7.11' apply false
    id "idea"
    id 'nu.studer.jooq' version '3.0.0'
}

ext {
    // 是否容器化部署
    def assemblyMode = System.getProperty("assemblyMode")
    def k8sMode = assemblyMode == "k8s"
    set("k8sMode", k8sMode)
    println("assembly mode: $assemblyMode")

    set("springVersion", "5.3.25")
    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-dependencies
    set("springBootVersion", "2.7.11")
    // https://mvnrepository.com/artifact/org.springframework.cloud/spring-cloud-dependencies
    set('springCloudVersion', "2021.0.5")
    set('springCloudOtelVersion', "1.1.3")
    set('swaggerVersion', "3.0.0")
    set('junitVersion', "5.5.2")
    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    set('lombokVersion', "1.18.10")
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    set('jacksonVersion', "2.15.4")
    set('jaxrsVersion', "2.0")
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-core
    // Fix CVE-2023-6378 1.2.11->1.2.13
    set('logbackVersion', "1.2.13")
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    set('slf4jVersion', "1.7.30")
    set('servletVersion', "3.0.1")
    // apache commons
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    set('apacheCommonsLang3Version', "3.12.0")
    // https://mvnrepository.com/artifact/org.apache.commons/commons-collections4
    set('apacheCommonsCollectionVersion', "4.4")
    // https://mvnrepository.com/artifact/org.apache.commons/commons-pool2
    set('apacheCommonsPoolVersion', "2.11.1")
    set('apacheCommonsCodecVersion', "1.14")
    set('apacheCuratorFrameworkVersion', "5.4.0")
    set('apacheCuratorRecipesVersion', "5.4.0")
    set('commonsCollectionsVersion', "3.2.2")
    set('dozerVersion', "5.5.1")
    set('jedisVersion', "3.0.1")
    // https://mvnrepository.com/artifact/org.jooq/jooq
    set('jooqVersion', "3.14.8")
    set('h2Version', "1.4.199")
    set('gradleJooqVersion', "2.0.11")
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
    set('apacheHttpClientVersion', "4.5.13")
    set('apacheThriftVersion', "0.15.0")
    // Fix CVE-2024-47554 CVE-2021-29425 2.6->2.14.0
    set('commonsIOVersion', "2.14.0")
    set('javaxServletVersion', "4.0.1")
    // Fix CVE-2023-2976 CVE-2020-8908 28.2-jre->32.0.0-android
    set('guavaVersion', "32.0.0-android")
    set('caffeineVersion', "2.9.3")
    set('jjwtVersion', "0.9.1")
    set('hibernateValidatorVersion', "6.1.4.Final")
    set('flapdoodleEmbeddedMongdbDBVersion', "4.4.0")
    set('jodaTimeVersion', "2.10.5")
    set('bcprovVersion', "1.70")
    set('reflectionsVersion', "0.10.2")
    set('cronUtilsVersion', "9.1.6")
    set('otelJdbcVersion', "1.24.0-alpha")
    set('commonsValidatorVersion', "1.6")
    set('okHttpVersion', "4.12.0")
    set('jcommanderVersion', "1.72")
    set('kubernetesJavaClientVersion', "11.0.4")
    set('springCloudKubernetesVersion', "2.0.6")
    set('cryptoJavaSDKVersion', "1.1.3")
    // Fix CVE-2019-10086,CVE-2014-0114
    set('commonsBeanutilsVersion', "1.9.4")
    if (System.getProperty("bkjobVersion")) {
        set('bkjobVersion', System.getProperty("bkjobVersion"))
        println "bkjobVersion:" + bkjobVersion
    } else {
        set('bkjobVersion', "1.0.0")
        println "bkjobVersion:" + bkjobVersion
    }
    set('bkAuditJavaSdkVersion', "1.0.8")
    set('mockitoVersion', "4.0.0")
    set('embeddedRedisVersion', "0.6")
    set('openai4jVersion', "0.18.0")
    // Fix CVE-2023-44981,CVE-2024-23944 3.7.1->3.8.4
    set('zookeeperVersion', "3.8.4")
    // Fix CVE-2023-3635 2.8.0->3.4.0
    set('okioVersion', "3.4.0")
    // Fix CVE-2023-34062 1.0.24->1.0.39
    set('reactorNettyHttpVersion', "1.0.39")
    set('reactorNettyCoreVersion', "1.0.39")
    // Fix CVE-2023-44487 4.1.84.Final->4.1.100.Final
    set('nettyCodecHttp2Version', "4.1.100.Final")
    // Fix CVE-2024-38809 CVE-2024-22262 CVE-2024-22259 CVE-2024-22243 5.3.23->5.3.39
    set('springWebVersion', "5.3.39")
    // Fix CVE-2021-0341 4.9.1->4.9.2
    set('okHttpVersion', "4.9.2")
    // Fix CVE-2023-46120 5.13.1->5.18.0
    set('amqpClientVersion', "5.18.0")
    // Fix CVE-2023-4759 5.13.1.202206130422-r->6.6.1.202309021850-r
    set('jgitVersion', "6.6.1.202309021850-r")

    // 使用Spring Boot内置的版本号变量，不采用驼峰命名，保持与官方BOM文件一致
    // Fix CVE-2023-22102 8.0.33->8.2.0
    set('mysql.version', "8.2.0")
    // Fix CVE-2023-44487, CVE-2024-52316
    set('tomcat.version', "9.0.96")
    // Fix CVE-2022-41854 CVE-2022-38752 CVE-2022-38751 CVE-2022-38749 CVE-2022-25857 CVE-2022-1471 1.29->1.33
    set('snakeyaml.version', "1.33")
}

group "com.tencent.bk.job"
version "${bkjobVersion}"

allprojects {
    group "com.tencent.bk.job"
    version "${bkjobVersion}"

    apply plugin: "idea"
    apply plugin: "eclipse"
    apply plugin: "java-library"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.vanniktech.dependency.graph.generator"

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.compilerArgs.add '-parameters'
    }

    repositories {
        mavenLocal()

        // 用户通过命令行方式指定的 maven repo
        def mavenRepoUrls = System.getProperty("mavenRepoUrl")
        if (mavenRepoUrls == null || mavenRepoUrls.trim().length() == 0) {
            // gradle.properties 中的 maven repo。优先级从高到低:
            // 1. command line, as set using -D.
            // 2. gradle.properties in GRADLE_USER_HOME directory. (<home directory of the current user>/.gradle by default)
            // 3. gradle.properties in the project’s directory, then its parent project’s directory up to the build’s root directory.
            // 4. gradle.properties in Gradle installation directory.
            mavenRepoUrls = project.findProperty("MAVEN_REPO_URL")
        }
        if (mavenRepoUrls != null && mavenRepoUrls.trim().length() > 0) {
            String[] repoUrls = mavenRepoUrls.trim().replace(" ", "").split(",")
            for (String repoUrl : repoUrls) {
                println("Add maven repo:" + repoUrl)
                maven { url repoUrl }
            }
        }
        mavenCentral()
        maven {
            url "https://repo.spring.io/snapshot"
        }
        maven {
            url "https://repo.spring.io/milestone"
        }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, "seconds"
            cacheDynamicVersionsFor 0, "seconds"
        }
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "idea"
    apply plugin: "eclipse"
    apply plugin: "io.spring.dependency-management"

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
    tasks.withType(Javadoc) {
        options.encoding("UTF-8")
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
        }
        include '**/*Test.class'
        exclude '**/*IntegrationTest.class'
    }
    task integrationTest(type: Test) {
        useJUnitPlatform()
        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
        }
        include '**/*IntegrationTest.class'
    }

    task testAll(type: Test) {
        useJUnitPlatform()
        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
        }
        include '**/*Test.class'
    }
    build.dependsOn testAll

    dependencyManagement {
        applyMavenExclusions = false
        dependencies {
            imports {
                mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
                mavenBom "org.springframework.cloud:spring-cloud-sleuth-otel-dependencies:${springCloudOtelVersion}"
            }
            // 由于spring cloud (2021.0.5)的 kubernetes 组件在特性版本jdk(8u252）下会出现不兼容（http2协议)，并且由于历史原因jdk版本无法升级，所以把spring cloud kubernetes 降低版本
            dependency "org.springframework.cloud:spring-cloud-kubernetes-client-autoconfig:$springCloudKubernetesVersion"
            dependency "org.springframework.cloud:spring-cloud-kubernetes-client-config:$springCloudKubernetesVersion"
            dependency "org.springframework.cloud:spring-cloud-kubernetes-client-discovery:$springCloudKubernetesVersion"
            dependency "org.springframework.cloud:spring-cloud-kubernetes-client-loadbalancer:$springCloudKubernetesVersion"
            dependency "org.springframework.cloud:spring-cloud-kubernetes-commons:$springCloudKubernetesVersion"
            dependency "io.kubernetes:client-java:$kubernetesJavaClientVersion"
            dependency "io.kubernetes:client-java-api:$kubernetesJavaClientVersion"
            dependency "io.kubernetes:client-java-api-fluent:$kubernetesJavaClientVersion"
            dependency "io.kubernetes:client-java-extended:$kubernetesJavaClientVersion"
            dependency "io.kubernetes:client-java-proto:$kubernetesJavaClientVersion"
            dependency "io.kubernetes:client-java-spring-integration:$kubernetesJavaClientVersion"

            dependency "org.junit.jupiter:junit-jupiter:$junitVersion"
            dependency "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
            dependency "org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion"
            dependency "org.springframework.boot:spring-boot-starter-quartz:$springBootVersion"
            dependency "org.springframework.boot:spring-boot-starter-data-redis:$springBootVersion"
            dependency "org.springframework.boot:spring-boot-starter-validation:$springBootVersion"
            dependency "org.springframework:spring-jdbc:$springVersion"
            dependency "org.springframework:spring-webflux:$springVersion"
            dependency "org.projectlombok:lombok:$lombokVersion"
            dependency "javax.ws.rs:javax.ws.rs-api:$jaxrsVersion"
            dependency "org.slf4j:slf4j-api:$slf4jVersion"
            dependency "io.springfox:springfox-boot-starter:$swaggerVersion"
            // apache commons
            dependency "org.apache.commons:commons-collections4:$apacheCommonsCollectionVersion"
            dependency "org.apache.commons:commons-pool2:$apacheCommonsPoolVersion"
            dependency "org.apache.commons:commons-lang3:$apacheCommonsLang3Version"
            dependency "org.apache.curator:curator-framework:$apacheCuratorFrameworkVersion"
            dependency "org.apache.curator:curator-recipes:$apacheCuratorRecipesVersion"
            dependency "commons-collections:commons-collections:$commonsCollectionsVersion"
            dependency "commons-io:commons-io:$commonsIOVersion"
            dependency "commons-codec:commons-codec:$apacheCommonsCodecVersion"
            dependency "commons-beanutils:commons-beanutils:$commonsBeanutilsVersion"
            dependency "net.sf.dozer:dozer:$dozerVersion"
            dependency "org.jedis:jedis:$jedisVersion"
            dependency "com.h2database:h2:$h2Version"
            dependency "javax.servlet:javax.servlet-api:$javaxServletVersion"
            // https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
            dependencySet(group: 'org.apache.httpcomponents', version: "$apacheHttpClientVersion") {
                entry "httpclient"
                entry "httpmime"
            }
            // https://mvnrepository.com/artifact/org.apache.thrift/libthrift
            dependency group: 'org.apache.thrift', name: 'libthrift', version: "$apacheThriftVersion"
            dependency group: 'com.google.guava', name: 'guava', version: "$guavaVersion"
            dependency "com.github.ben-manes.caffeine:caffeine:$caffeineVersion"
            dependency group: 'io.jsonwebtoken', name: 'jjwt', version: "$jjwtVersion"
            dependency "net.sourceforge.jchardet:jchardet:1.0"
            dependency "de.flapdoodle.embed:de.flapdoodle.embed.mongo.spring26x:$flapdoodleEmbeddedMongdbDBVersion"
            dependency "de.flapdoodle.embed:de.flapdoodle.embed.mongo:$flapdoodleEmbeddedMongdbDBVersion"
            dependency "joda-time:joda-time:$jodaTimeVersion"
            dependency "org.bouncycastle:bcprov-jdk15on:$bcprovVersion"
            // https://github.com/ronmamo/reflections
            dependency "org.reflections:reflections:$reflectionsVersion"
            dependency "com.cronutils:cron-utils:$cronUtilsVersion"
            dependency "commons-validator:commons-validator:$commonsValidatorVersion"
            dependency "io.opentelemetry.instrumentation:opentelemetry-jdbc:$otelJdbcVersion"
            dependency "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
            dependency "com.squareup.okhttp3:okhttp:$okHttpVersion"
            dependency "com.squareup.okhttp3:okhttp-sse:$okHttpVersion"
            dependencySet(group: "org.jooq", version: "$jooqVersion") {
                entry "jooq"
                entry "jooq-codegen"
                entry "jooq-meta"
            }
            dependencySet(group: "com.fasterxml.jackson", version: "$jacksonVersion") {
                entry "jackson-core"
                entry "jackson-databind"
                entry "jackson-annotations"
            }
            dependency "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
            dependency "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jacksonVersion"
            dependencySet(group: 'ch.qos.logback', version: "$logbackVersion") {
                entry "logback-core"
                entry "logback-classic"
            }
            dependencySet(group: 'org.hibernate.validator', version: "$hibernateValidatorVersion") {
                entry "hibernate-validator"
            }
            dependency "com.beust:jcommander:$jcommanderVersion"
            dependency "com.tencent.bk.sdk:crypto-java-sdk:$cryptoJavaSDKVersion"
            dependency "com.tencent.bk.sdk:spring-boot-bk-audit-starter:$bkAuditJavaSdkVersion"
            dependency "org.mockito:mockito-inline:$mockitoVersion"
            dependency "com.github.kstyrc:embedded-redis:$embeddedRedisVersion"
            dependency "dev.ai4j:openai4j:$openai4jVersion"
            dependency "org.apache.zookeeper:zookeeper:$zookeeperVersion"
            dependency "com.squareup.okio:okio:$okioVersion"
            dependency "io.projectreactor.netty:reactor-netty-http:$reactorNettyHttpVersion"
            dependency "io.projectreactor.netty:reactor-netty-core:$reactorNettyCoreVersion"
            dependency "io.netty:netty-codec-http2:$nettyCodecHttp2Version"
            dependency "org.springframework:spring-web:$springWebVersion"
            dependency "com.squareup.okhttp3:okhttp:$okHttpVersion"
            dependency "com.rabbitmq:amqp-client:$amqpClientVersion"
            dependency "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
        }
    }
    dependencies {
        constraints {
            implementation('commons-fileupload:commons-fileupload:1.6.0') {
                because 'version 1.4 pulled from spring-cloud-starter-openfeign has vulnerabilities(CVE-2023-24998)'
            }
        }
    }

    configurations {
        all*.exclude group: 'junit', module: 'junit'
        all*.exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        all*.exclude group: 'org.springframework.cloud', module: 'spring-cloud-sleuth-brave'
        all*.exclude group: 'org.springframework.data', module: 'spring-data-redis'
    }
}
apply plugin: TaskTreePlugin
